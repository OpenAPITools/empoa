buildscript {
  configurations.classpath {
    resolutionStrategy {
      //This is required by 'git-publish' version '3.0.0':
      force 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
  }
}

plugins {
    id 'com.diffplug.spotless' version '6.0.2' apply false
    id 'org.asciidoctor.jvm.convert' version '3.3.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
    id 'de.marcphilipp.nexus-publish' version '0.4.0' apply false
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.22.0'
}

wrapper {
    gradleVersion = '6.7.1'
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'de.marcphilipp.nexus-publish'

    group = 'org.openapitools.empoa'
}

subprojects {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            importOrder 'java', 'javax', 'org', 'com', ''
            removeUnusedImports()

            // available versions are: https://github.com/diffplug/spotless/tree/master/lib-extra/src/main/resources/com/diffplug/spotless/extra/eclipse_jdt_formatter
            eclipse('4.13.0').configFile('.settings/org.eclipse.jdt.core.prefs')
        }
    }

    repositories {
        // mavenLocal()
        mavenCentral()
        jcenter()
        // maven {
        //     url "http://dl.bintray.com/jmini/maven/"
        // }
    }

    nexusPublishing {
        repositories {
            sonatype {
                username = project.findProperty('sonatypeUser') ?: ''
                password = project.findProperty('sonatypePassword') ?: ''
            }
        }
    }
}

nexusStaging {
    packageGroup = 'org.openapitools'
    username = project.findProperty('sonatypeUser') ?: ''
    password = project.findProperty('sonatypePassword') ?: ''
}

def buildDate() {
    return new Date().format('yyyy-MM-dd')
}

configurations {
    asciidoctorExtensions
}

asciidoctorj {
    version = "$asciidoctorjVersion"
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    sourceDir = file('docs')
    baseDirFollowsSourceFile()
    outputDir = file('build/docs/html5')
    attributes = ['revdate'           : "${buildDate()}",
                  'project-version'   : "$version",
                  'artifacts-version' : "$lastVersion",
                  'mp-openapi-version': "$mpOpenAPIVersion",
                  'gh-repo-owner'     : "$githubRepositoryOwner",
                  'gh-repo-name'      : "$githubRepositoryName",
                  'source-highlighter': 'coderay',
                  'imagesdir'         : '',
                  'toc'               : 'left',
                  'toclevels'         : '3',
                  'icons'             : 'font',
                  'sectanchors'       : 'true',
                  'idprefix'          : '',
                  'idseparator'       : '-',
                  'docinfo1'          : 'true']
    repositories {
        mavenCentral()
    }
    dependencies {
        asciidoctorExtensions 'fr.jmini.asciidoctorj:git-link:3.2.1'
    }
}

gitPublish {
    repoUri = 'git@github.com:' + "$githubRepositoryOwner" + '/' + "$githubRepositoryName" + '.git'
    branch = 'gh-pages'

    contents {
        from "${file('build/docs/html5')}"
    }

    preserve {
        include '.nojekyll'
    }

    commitMessage = "Update the 'gh-pages' branch."
}

release {
    buildTasks = ['releaseBuild']
}

task releaseBuild {
    dependsOn(
            'checkLastVersionValue',
            'clean',
            'build',
            'publish',
            'asciidoctor',
            'gitPublishPush'
    )
}

task checkLastVersionValue {
    doLast {
        if(version.endsWith('SNAPSHOT')) {
            throw new GradleException("version '$version' ends with SNAPSHOT, this is not a release build!")
        }
        if(lastVersion != version) {
            throw new GradleException("lastVersion '$lastVersion' does not match version '$version', fix it in the 'gradle.properties' file.")
        }
    }
}
